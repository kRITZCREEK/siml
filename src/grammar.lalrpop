use crate::token::Token;
use crate::expr::{Expr, ParserExpr, Literal, Declaration, DataConstructor};
use crate::types::{Type};
use crate::bi_types;

grammar;

extern {
    enum Token {
        "identifier" => Token::Ident(<String>),
        "int_literal" => Token::IntLiteral(<i32>),
        "bool_literal" => Token::BooleanLiteral(<bool>),
        "forall" => Token::Forall,
        "let" => Token::Let,
        "type" => Token::Type,
        "in" => Token::In,
        "=" => Token::Equals,
        "\\" => Token::Lambda,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "." => Token::Dot,
        "->" => Token::Arrow,
        ":" => Token::Colon,
        "," => Token::Comma,
        ";" => Token::Semi,
    }
}

pub Program: Vec<Declaration<String>> = {
  Declaration+
}

pub Declaration: Declaration<String> = {
 "let" <name: "identifier"> ":" <ty: BiType> "=" <expr: Expr> ";" =>
   Declaration::Value{ expr: Expr::Ann { expr: Box::new(expr), ty }, name: name.to_string() },
 "type" <name: "identifier"> "(" <constructors: CommaSep<DataConstructor>> ")" ";" =>
   Declaration::Type { name: name.to_string(), constructors }
}

DataConstructor: DataConstructor = {
  <name: "identifier"> => DataConstructor { name: name.to_string() }
}

pub Expr: ParserExpr = {
    ExprAtom,
    "\\" <binder: "identifier"> "." <body: Expr> =>
        Expr::Lambda { binder: binder.to_string(), body: Box::new(body) },
    "let" <binder: "identifier"> "=" <expr: Expr> "in" <body: Expr> =>
        Expr::Let { binder: binder.to_string(), expr: Box::new(expr), body: Box::new(body) },
    <function: ExprAtom> <arguments: ExprAtom+> => {
        arguments
          .into_iter()
          .fold(function, |acc, x| Expr::App { func: Box::new(acc), arg: Box::new(x) })
    },
}

ExprAtom: ParserExpr = {
    "identifier" => Expr::Var(<>.to_string()),
    "int_literal" => Expr::Literal(Literal::Int(<>)),
    "bool_literal" => Expr::Literal(Literal::Bool(<>)),
    "(" <e:Expr> ":" <t:BiType> ")" => Expr::Ann{ ty: t, expr: Box::new(e) },
    "(" <fst:Expr> "," <snd:Expr> ")" => Expr::Tuple(Box::new(fst), Box::new(snd)),
    "(" <e:Expr> ")" => e,
};

pub Type: Type = {
    BiType => {
        Type::from_bi_type(<>)
    }
}

pub BiType: bi_types::Type = {
    "forall" <vars: "identifier"+> "." <ty: BiType> => {
        bi_types::Type::Poly {vars, ty: Box::new(ty)}
    },
    <arg: TypeAtom> "->" <result: BiType> => {
        bi_types::Type::Fun {arg: Box::new(arg), result: Box::new(result)}
    },
    TypeAtom
}

TypeAtom: bi_types::Type = {
    "(" <t:BiType> ")" => t,
    "(" <fst:BiType> "," <snd:BiType> ")" => bi_types::Type::Tuple(Box::new(fst), Box::new(snd)),
    <i:"identifier"> => {
      if i.chars().next().unwrap().is_lowercase() {
          bi_types::Type::Var(i)
      } else {
          bi_types::Type::Constructor(i)
      }
    }
}

// Also allows trailing commas
CommaSep<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};
